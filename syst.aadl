package Syst
public
	system SecuritySystem
	end SecuritySystem;

	system implementation SecuritySystem.with_devices
		subcomponents
			this_motion_sensor: device MotionSensor;
			this_magnetic_alarm_sensor: device MagneticAlarmSensor;
			this_camera: device Camera;
			this_alarm_unit: device AlarmUnit;
			this_controller: process controller.with_threads;
			this_bus: bus HWConnection.impl;
			this_processor: processor MainProcessor.impl;
		connections
			motion_conn: port this_motion_sensor.motion_detected -> this_controller.motion_in;
			magnetic_conn: port this_magnetic_alarm_sensor.alarm_set_off -> this_controller.magnetic_in;
			camera_conn_out: port this_controller.camera_out -> this_camera.camera_on_off;
			alarm_conn: port this_controller.alarm_out -> this_alarm_unit.alarm_on_off;
			camera_conn_in: port this_camera.video_stream -> this_controller.video_from_camera;
					-- bus connections
			bus_motion: bus access this_bus -> this_motion_sensor.bus_access;
			bus_magnetic: bus access this_bus -> this_magnetic_alarm_sensor.bus_access;
			bus_camera: bus access this_bus -> this_camera.bus_access;
			bus_alarm: bus access this_bus -> this_alarm_unit.bus_access;
			bus_processor: bus access this_bus -> this_processor.bus_access;
		flows
			SecuritySystem_flow: end to end flow this_magnetic_alarm_sensor.magneticAlarmSensor_Source -> magnetic_conn -> this_controller.Controller_path ->alarm_conn ->this_alarm_unit.AlarmUnit_sink {Latency => 700ms .. 900 ms;};
		properties
			Allowed_Processor_Binding => (reference (this_processor)) applies to this_controller.this_thread_motion;
			Allowed_Processor_Binding => (reference (this_processor)) applies to this_controller.this_thread_magnetic;
			Allowed_Processor_Binding => (reference (this_processor)) applies to this_controller.this_thread_Control;
			Allowed_Processor_Binding => (reference (this_processor)) applies to this_controller.this_thread_Video;
	end SecuritySystem.with_devices;

	process Controller
		features
			motion_in: in event port;
			magnetic_in: in event port;
			camera_out: out event port;
			alarm_out: out event port;
			video_from_camera: in data port;
		flows 
			Controller_path: flow path magnetic_in ->alarm_out;
	end Controller;

	process implementation Controller.with_threads
		subcomponents
			this_thread_motion: thread ReadMotion.impl;
			this_thread_magnetic: thread ReadMagnetic.impl;
			this_thread_control: thread ControlRules.impl;
			this_thread_video: thread ProcessVideo.impl;
		connections
			motion_thread_conn: port motion_in -> this_thread_motion.motion_in;
			magnetic_thread_conn: port magnetic_in -> this_thread_magnetic.magnetic_in;
			motion_control_conn: port this_thread_motion.motion_out -> this_thread_control.motion_in;
			magnetic_control_conn: port this_thread_magnetic.magnetic_out -> this_thread_control.magnetic_in;
			camera_control_conn: port this_thread_control.camera_out -> camera_out;
			alarm_control_conn: port this_thread_control.alarm_out -> alarm_out;
			video_conn: port video_from_camera -> this_thread_video.video_in;
		flows
			Controller_path : flow path magnetic_in -> magnetic_thread_conn -> this_thread_magnetic.ReadMagnetic_path1 ->magnetic_control_conn ->
			this_thread_control.ControlRules_path2 ->alarm_control_conn -> alarm_out;
	end Controller.with_threads;

	thread ReadMotion
		features
			motion_in: in event port;
			motion_out: out event port;
		properties
			Dispatch_Protocol => Periodic;
			Compute_Execution_Time => 3ms .. 5ms;
			Period => 50ms;
			Priority => 2;
	end ReadMotion;

	thread implementation ReadMotion.impl
	end ReadMotion.impl;

	thread ReadMagnetic
		features
			magnetic_in: in event port;
			magnetic_out: out event port;
		flows 
			ReadMagnetic_path1 : flow path magnetic_in -> magnetic_out;
		properties
			Dispatch_Protocol => Periodic;
			Compute_Execution_Time => 3ms .. 5ms;
			Period => 50ms;
			Priority => 3;
	end ReadMagnetic;

	thread implementation ReadMagnetic.impl
	end ReadMagnetic.impl;

	thread ControlRules
		features
			motion_in: in event port;
			magnetic_in: in event port;
			camera_out: out event port;
			alarm_out: out event port;
		flows
			ControlRules_path2 : flow path magnetic_in ->alarm_out;
		properties
			Dispatch_Protocol => Periodic;
			Compute_Execution_Time => 3ms .. 5ms;
			Period => 50ms;
			Priority => 1;
	end ControlRules;

	thread implementation ControlRules.impl
	end ControlRules.impl;

	thread ProcessVideo
		features
			video_in: in data port;
		properties
			Dispatch_Protocol => Periodic;
			Compute_Execution_Time => 3ms .. 5ms;
			Period => 50ms;
			Priority => 4;
	end ProcessVideo;

	thread implementation ProcessVideo.impl
	end ProcessVideo.impl;

	processor MainProcessor
		features
			bus_access: requires bus access HWConnection;
	end MainProcessor;

	processor implementation MainProcessor.impl
		subcomponents
			this_ram: memory ram;
		properties
			Preemptive_Scheduler => true;
			Scheduler_Quantum => 2ms;
	end MainProcessor.impl;

	memory Ram
	end Ram;

	bus HWConnection
	end HWConnection;

	bus implementation HWConnection.impl
	end HWConnection.impl;

	device MotionSensor
		features
			motion_detected: out event port;
			bus_access: requires bus access HWConnection;
	end MotionSensor;

	device implementation MotionSensor.impl
	end MotionSensor.impl;

	device MagneticAlarmSensor
		features
			alarm_set_off: out event port;
			bus_access: requires bus access HWConnection;
		flows
			magneticAlarmSensor_Source: flow source alarm_set_off;
	end MagneticAlarmSensor;

	device implementation MagneticAlarmSensor.impl
	end MagneticAlarmSensor.impl;

	device Camera
		features
			camera_on_off: in event port;
			video_stream: out data port;
			bus_access: requires bus access HWConnection;
	end Camera;

	device implementation Camera.impl
	end Camera.impl;

	device AlarmUnit
		features
			alarm_on_off: in event port;
			bus_access: requires bus access HWConnection;
		flows
			AlarmUnit_sink: flow sink alarm_on_off;
	end AlarmUnit;

	device implementation AlarmUnit.impl
	end AlarmUnit.impl;
end Syst;





